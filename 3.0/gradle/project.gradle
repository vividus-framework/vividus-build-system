/*
 * Copyright 2019-2024 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    repositories {
        mavenCentral()
        gradlePluginPortal()
    }
    dependencies {
        classpath(group: 'pl.allegro.tech.build', name: 'axion-release-plugin', version: '1.18.17')
        classpath(group: 'com.diffplug.spotless', name: 'spotless-plugin-gradle', version: '7.0.3')
    }
}
apply plugin: pl.allegro.tech.build.axion.release.ReleasePlugin

scmVersion {
    versionCreator('simple')
    repository {
        directory.set(project.rootProject.file(project.findProperty('gitRepository.rootDirectory') ?: './').absolutePath)
    }
    tag {
        prefix = 'release-'
    }
}

// Added to exclude files mentioned in .gitignore
//https://github.com/diffplug/spotless/issues/365
def prefix = '**/'
ext.gitignorePatterns = rootProject.file('.gitignore')
    .readLines()
    .findAll({ !it.startsWith('#') && !it.isEmpty() })
    .collect({ it.startsWith(prefix) ? it : prefix + it })


allprojects {
    buildDir = file(System.env.OUTPUT_ROOT ? "${System.env.OUTPUT_ROOT}/${project.name}" : 'output')
    ext.gitRepo = new File(scmVersion.repository.directory.get(), '.git')
    try {
        project.version = scmVersion.version
    }
    catch (pl.allegro.tech.build.axion.release.domain.scm.ScmException e) {
        project.version = 'local-SNAPSHOT'
    }
    apply plugin: com.diffplug.gradle.spotless.SpotlessPlugin

    spotless {
        format 'documentation', {
            target '**/*.md', '**/*.adoc'
            targetExclude gitignorePatterns

            trimTrailingWhitespace()
            leadingTabsToSpaces()
            endWithNewline()
        }
        format 'misc', {
            target '**/*.gradle', '**/.gitignore', '**/*.xml', '**/*.json', '**/*.properties'
            targetExclude gitignorePatterns

            trimTrailingWhitespace()
            leadingTabsToSpaces()
            endWithNewline()
        }
    }
}

repositories {
    mavenCentral()
}
