/*
 * Copyright 2019-2023 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    repositories {
        gradlePluginPortal()
    }
    dependencies {
        classpath(group: 'pl.allegro.tech.build', name: 'axion-release-plugin', version: '1.14.4')
        classpath(group: 'com.diffplug.spotless', name: 'spotless-plugin-gradle', version: '6.18.0')
    }
}

services.get(org.gradle.internal.logging.text.StyledTextOutputFactory)
    .create("error-output")
    .style(org.gradle.internal.logging.text.StyledTextOutput.Style.Failure)
    .println('')
    .println('=============================================================================================================================')
    .println('')
    .println('      VIVIDUS Build System 1.0 is not maintained anymore.      ')
    .println('      Please, upgrade to VIVIDUS Build System 2.0 at the earliest convenient time.      ')
    .println('      Migration guide is available at https://github.com/vividus-framework/vividus-build-system#migrating-from-10-to-20      ')
    .println('')
    .println('=============================================================================================================================')
    .println('')

apply plugin: pl.allegro.tech.build.axion.release.ReleasePlugin

def customVersionPrefix = project.findProperty('versionPrefix') ?: ''
scmVersion {
    tag {
        if (customVersionPrefix.length() > 0) {
            prefix = 'releases/' + customVersionPrefix
        } else {
            prefix = 'release-'
        }
    }
}

def customPrefixWithSeparator = customVersionPrefix.length() > 0 ? customVersionPrefix + '-' : ''

// Added to exclude files mentioned in .gitignore
//https://github.com/diffplug/spotless/issues/365
def prefix = '**/'
ext.gitignorePatterns = rootProject.file('.gitignore')
    .readLines()
    .findAll({ !it.startsWith('#') && !it.isEmpty() })
    .collect({ it.startsWith(prefix) ? it : prefix + it })


allprojects {
    buildDir = file(System.env.OUTPUT_ROOT ? "${System.env.OUTPUT_ROOT}/${project.name}" : 'output')
    ext.gitRepo = new File(scmVersion.repository.directory.asFile.get(), '.git')
    try {
        project.version = customPrefixWithSeparator + scmVersion.version
    }
    catch (pl.allegro.tech.build.axion.release.domain.scm.ScmException e) {
        project.version = 'local-SNAPSHOT'
    }
    apply plugin: com.diffplug.gradle.spotless.SpotlessPlugin

    spotless {
        format 'documentation', {
            target '**/*.md', '**/*.adoc'
            targetExclude gitignorePatterns

            trimTrailingWhitespace()
            indentWithSpaces()
            endWithNewline()
        }
        format 'misc', {
            target '**/*.gradle', '**/.gitignore', '**/*.xml', '**/*.json', '**/*.properties'
            targetExclude gitignorePatterns

            trimTrailingWhitespace()
            indentWithSpaces()
            endWithNewline()
        }
    }
}

repositories {
    mavenCentral()
}
